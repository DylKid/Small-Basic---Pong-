'Initialzie variables
height = 500 
width = 1000

paddleHeight = 100 
paddleWidth = 30
paddleBorderWidth = 3

ballRadius = 26
ballX = (width/2) - ballRadius
ballY = (height/2) - ballRadius

'Make sure the change in velocity is between 0.4 and 1, but a little random to keep life fresh 
ballDeltaX = (Math.GetRandomNumber(10)/10) 
ballDeltaY = (Math.GetRandomNumber(10)/10)
If(ballDeltaX < 0.4) Then 
  ballDeltaX = 0.4 
EndIf
If(ballDeltaY < 0.4) Then 
ballDeltaY = 0.4 
EndIf 

'Draw the paddles
GraphicsWindow.PenWidth = paddleBorderWidth 

'The Left Paddle (blue) 
GraphicsWindow.PenColor = "#00008B" 
GraphicsWindow.BrushColor = "#0000FF"
paddleLeft = Shapes.AddRectangle(paddleWidth, paddleHeight) 
paddleLeftY = (height/2) - (paddleHeight/2)
paddleLeftX = 10

'The Right Paddle (Red) 
GraphicsWindow.PenColor = "#8B0000"
GraphicsWindow.BrushColor = "#B22222" 
paddleRight = Shapes.AddRectangle(paddleWidth, paddleHeight) 
paddleRightY = (height/2) - (paddleHeight/2)
paddleRightX = width - (paddleWidth + 10)

'The Ball 
GraphicsWindow.PenColor = "Peru" 
GraphicsWindow.BrushColor = "Chocolate" 
ball = Shapes.AddEllipse(ballRadius, ballRadius) 
Shapes.Move(ball, ballX, ballY) 

'Initialize the window 
InitializeWindow()

'Main game loop 
While(1 = 1)
  
  GraphicsWindow.KeyDown = OnKeyDown 
  
  'Update the ball's position based on the X and Y velocity 
  ballX = ballX + ballDeltaX 
  ballY = ballY + ballDeltaY 
  
  'Test ball collision with the borders 
  If(ballX >= width - ballRadius Or ballX <= 0) Then 
    ballDeltaX = -ballDeltaX
  EndIf 
  
  If(ballY <= 0 Or ballY >= height - ballRadius) Then 
    ballDeltaY = -ballDeltaY
  EndIf 
  
  'Test ball collision with paddles 
  If(ballX >= (paddleRightX-paddleWidth) And ((ballY <= paddleRightY) And (ballY >= paddleRightY + paddleHeight)))  Then 
    ballDeltaX = -ballDeltaX
  EndIf 
  
  Program.Delay(5) 
  
  Shapes.Move(ball, ballX, ballY) 
  
EndWhile

'Put it in a function just to neaten up the start 
Sub InitializeWindow
  'Draw the border 
  GraphicsWindow.PenColor = "Black" 
  GraphicsWindow.PenWidth = 5
  GraphicsWindow.Height = height
  GraphicsWindow.Width = width 
  GraphicsWindow.DrawRectangle(0,0,width,height) 
  Shapes.Move(paddleLeft, paddleLeftX, paddleLeftY)
  Shapes.Move(paddleRight, paddleRightX, paddleRightY) 
EndSub

'Handle the controls   
Sub OnKeyDown 
  'Handle the 'left' player 
  If GraphicsWindow.LastKey = "A" Then 
    paddleLeftY = paddleLeftY - 8
  ElseIf GraphicsWindow.LastKey = "Z" Then 
    paddleLeftY = paddleLeftY + 8 
  EndIf 
  Shapes.Move(paddleLeft, paddleLeftX, paddleLeftY)
    
  'Handle the 'Right' player 
  If GraphicsWindow.LastKey = "Up" Then
    paddleRightY = paddleRightY - 8 
  ElseIf GraphicsWindow.LastKey = "Down" Then
    paddleRightY = paddleRightY + 8 
  EndIf
  Shapes.Move(paddleRight, paddleRightX, paddleRightY) 
EndSub 
 